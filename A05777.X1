	.TITLE C & R 11-MAR-76 TAH
	.ASECT
	.=11000

PAULA=160		;160-163
PAULB=164		;164-167
PAULC=170		;170-173
PAULD=174		;174-177
GTIMA=124		;LSD
GTIMB=125		;MSD
EOT=377			;EOM CODE
MESO=122		;122-123 MESOUT ONLY
MESI=120		;120-121 MESOUT ONLY
LINEHD=117
PV=5400				;B00
PS=5000				;A00
PD=4400				;900
BP=4000				;800
PF=6000				;C00
SYNC=10000
PCB0=0
PCB1=20
PCB2=40
AT=375
SING=376
COINS=377
AMT=214
AMNUM=215
GOTYM=216
GOFLG=217
COIN1=10026
COIN2=10032
P1SW=10002
GTSET=10022
PFIRE=10022
PBIT=101
PNBIT=102
PNUM=103
TIME=104
PBNBIT=106
PBBIT=107
R0=200
R1=201
R2=202
R3=203
R4=204
R5=205
R6=206
R7=207
PAUL=214		;(214-217)
PAULV=3400
PAULS=3000
PIACA=10001
PIADA=10000
PIACB=10003
PIADB=10002
SVSP=210
SHOT=212
HIT=213
PCB3=60
ZING=116
CRSH=115
SCH=114
SCR=371			;371-374 FOR SCORE DIGITS
MOTOR=2400
SCREE=2404
CRASH=2405
START1=2406
AUDIO=2407


START:	SEI
	LDA I,0
	STA A,PIACA
	STA A,PIACB
	LDA I,177
	STA A,PIADA
	LDA I,0
	STA A,PIADB
	LDA I,4
	STA A,PIACA
	STA A,PIACB
	LDX I,370
	TXS
	LDA I,0
	PHA
	PLP
	JSR SCRINT		;INIT. SCORES TO "00"
DONE:	LDY I,10		;INITIALIZE SOUND BITS
	LDA I,377
	STA AY,MOTOR-1
	LSR
	DEY
	BNE S,-5
ATRCYC:	JSR CLRP
	LDA I,200
	STA Z,PCB0
	STA Z,PCB1
	STA Z,PCB2
	STA Z,PCB3
	LDA I,0
	STA Z,COINS
	STA Z,SING
	LDA I,200
	STA Z,AT
	JSR CONTIN-3
	LDX I,30		;ATARI TO SCREEN
	JSR MESOUT
	LDX I,46
	JSR MESOUT
	LDX I,34		;NUMBER OF PLAYERS
	JSR MESOUT
	LDX I,36		;PLAYER PLAYS AGAINST MACHINE
	JSR MESOUT
	LDX I,42
	JSR MESOUT
	LDX I,44
	JSR MESOUT
	LDA A,GTSET
	AND I,3
	ASL
	ADC I,20
	TAX
	JSR MESOUT		;GAME COST TO SCREEN
ATLOOP:	LDA Z,GOTYM
	BNE CNSRCH
	LDA Z,GOFLG
	BMI GOOUT
	LDX I,0
	JSR MESOUT		;GAME OVER
	LDA I,200
	STA Z,GOFLG
	BNE GOTIMR
GOOUT:	LDX I,10
	JSR MESOUT
	LDA I,0
	STA Z,GOFLG
GOTIMR:	LDA I,20
	STA Z,GOTYM
CNSRCH:	JSR LP1			;ONE FRAME OF GAME DISPLAY
	JSR COININ
	LDA Z,COINS
	BNE ATEND
	DEC Z,GOTYM
	JMP ATLOOP
ATEND:	JSR CLRP		;CLEAR SCREEN AND RAM
	LDA I,0
	STA Z,AT		;CLEAR ATTRACT FLAG
	LDA A,GTSET
	AND I,3
	CMP I,1
	BEQ STRTCK
	LDX I,14		;INSERT COIN FOR MORE PLAYERS
	JSR MESOUT
	LDA Z,COINS
	CMP I,2
	BCS MULTC
	LDA A,GTSET
	AND I,3
	CMP I,2
	BEQ STRTCK
MULTC:	LDX I,50
	JSR MESOUT
	LDA I,0
	STA A,START1
	JMP STRKC
STRTCK:	LDX I,12		;ONE OR MUL
	JSR MESOUT
	LDA I,0
	STA A,START1
	LDA A,PIADA
	AND I,277
	STA A,PIADA
STRKC:	LDA A,GTSET
	AND I,3
	BNE NRMST
	LDA Z,COINS
	CMP I,2
	BCS NRMST
	JSR CONIN
	JMP STRTCK
NRMST:	LDA A,COIN1		;TEST START SINGLE
	AND I,100
	BNE STRT2
	LDA Z,COINS
	CMP I,2
	BCC STRT1
	LDA A,GTSET
	AND I,3
	BNE STRT2
	LDA Z,COINS
	CMP I,2
	BNE STRT2
STRT1:	LDA I,200
	STA Z,SING
	JSR GAMELP
	JMP DONE
STRT2:	LDA Z,COINS
	CMP I,2
	BCC STRT2A
	LDX I,52
	JSR MESOUT
	LDA I,1
	STA A,START1
	LDA A,PIADA
	AND I,277
	STA A,PIADA
STRT2A:	LDA A,COIN2		;TEST MULTIPLE START
	AND I,100
	BEQ ST2UP
CNLOOK:	JSR CONIN
	JMP NRMST
ST2UP:	LDA A,GTSET
	AND I,3
	BNE ST2GO
	LDA Z,COINS
	CMP I,4
	BCC CNLOOK
	AND I,1
	BNE CNLOOK
	BEQ ST2ON
ST2GO:	CMP I,3
	BNE ST2ON
	LDA Z,COINS
	CMP I,1
	BEQ CNLOOK
ST2ON:	LDA I,0
	STA Z,SING
	JSR GAMELP
	JMP DONE


GAMELP:	LDA I,1
	STA A,START1
	JSR CLRP
	LDA Z,SING
	BPL S,+7
	LDX I,32
	JSR MESOUT
	LDA I,300		;DELAY FOR GAS PEDAL MESSAGE
	STA Z,R0
	LDX I,40		;WHEN CAR HIT
	JSR MESOUT
	LDX I,16		;GAS PEDAL
	JSR MESOUT
	DEC Z,R0
	BNE S,-7
	JSR CLRP
	LDA A,GTSET
	AND I,14
	EOR I,14
	LSR
	LSR
	TAX
	LDA I,60
	STA Z,GTIMA
	LDA AX,TYMTBL
	STA Z,GTIMB		;SET GAME TIMER
	LDA Z,SING
	BPL NOSING
	LDA I,200
	STA Z,PCB0
	STA Z,PCB2
	STA Z,PCB3
	JMP CONTIN-3
NOSING:	LDA A,GTSET
	AND I,3
	BEQ CK251-2
	CMP I,3
	BEQ CK251
	BNE CK252
	LSR Z,COINS
CK251:	LDX Z,COINS
	LDA I,200
	STA Z,PCB3
	DEX
	STA Z,PCB0
	DEX
	BEQ CONTIN-3
	STA Z,PCB1
	DEX
	BEQ CONTIN-3
	STA Z,PCB2
	JMP CONTIN-3
CK252:	CMP I,2
	BNE CK254
	ASL Z,COINS
	BNE CK251
CK254:	LDA I,4
	STA Z,COINS
	BNE CK251
	JSR INITP
CONTIN:	LDX I,2			;COPS
	JSR MESOUT
	LDX I,4			;ROBBERS
	JSR MESOUT
	LDA Z,AT
	BMI LP1
	LDX I,6			;TIMER
	JSR MESOUT
	LDA I,0
	STA A,AUDIO		;SOUNDS ENABLED
	STA Z,CRSH
	STA Z,SCH
	JSR SCRINT
	LDA A,MES30
	CMP A,ODDS-1
	BEQ S,+3
	BRK
LP1:	JSR OUTOBJ		;OUTPUT NEW INFO
	JSR SWIT		;INPUT SWITCHES
	DEC Z,TIME+1
	LDA Z,AT
	BMI BTST
	LDA Z,TIME+1		;TIMER DEC EVERY 2 SEC.
	AND I,177
	BNE BTST
	LDA Z,GTIMA
	CMP I,60
	BNE DECA
	LDA Z,GTIMB
	CMP I,60
	BNE S,+10
	LDA I,1
	STA A,AUDIO		;SOUND OFF
	RTS
	DEC Z,GTIMB
	LDA I,72
	STA Z,GTIMA
DECA:	DEC Z,GTIMA
BTST:	JSR COININ
	LDA Z,PAULB
	BMI CKPC
	LDA Z,SCR+1
	CMP I,61
	BEQ SETPB
	LDA Z,SCR+3
	CMP I,61
	BNE BBTST
SETPB:	LDA I,1
	STA Z,PAULB
	BNE BBTST
CKPC:	LDA Z,PAULC
	BMI CKPD
	LDA Z,SCR+1
	CMP I,62
	BEQ SETPC
	LDA Z,SCR+3
	CMP I,62
	BNE BBTST
SETPC:	LDA I,1
	STA Z,PAULC
	BNE BBTST
CKPD:	LDA Z,PAULD
	BMI BBTST
	LDA Z,SCR+1
	CMP I,63
	BEQ SETPD
	LDA Z,SCR+3
	CMP I,63
	BNE BBTST
SETPD:	LDA I,1
	STA Z,PAULD
BBTST:	LDX I,PAULB
	LDA I,0
	STA Z,PNUM
PBBTST:	LDA ZX,0
	BMI ADVPAL
	BEQ ADVPAL
	LDY Z,PNUM
	LDA AY,PALST
	CMP Z,PAULA+1
	BNE ADVPAL
	LDA A,PIADA		;ENABLE BUS
	ORA I,40
	STA A,PIADA
	LDA I,200
	STA ZX,0
	LDY I,340
	STY ZX,1
	LDA I,377
	STA ZX,2
	LDA I,20
	STA AY,PV
	INY
	BNE S,-4
	LDA A,PIADA
	AND I,337
	STA A,PIADA
ADVPAL:	CPX I,PAULD
	BEQ BBBTST
	INC Z,PNUM
	INX
	INX
	INX
	INX
	JMP PBBTST
BBBTST:	LDA A,MES30+1
	CMP A,MES0
	BEQ S,+3
	BRK
	LDA Z,TIME+1
	AND I,17
	BNE HITSND
	LDA I,1
	STA Z,PBIT
	LDA I,0
	STA Z,PNUM
	LDX I,PCB0
	JSR SOUND
	ASL Z,PBIT
	INC Z,PNUM
	LDX I,PCB1
	JSR SOUND
	ASL Z,PBIT
	INC Z,PNUM
	LDX I,PCB2
	JSR SOUND
	ASL Z,PBIT
	INC Z,PNUM
	LDX I,PCB3
	JSR SOUND
HITSND:	LDA A,MES30+2
	CMP A,SETPB+1
	BEQ S,+3
	BRK
	LDA Z,SCH
	BNE S,+6
	LDA I,1		;SOUND OFF
	BNE HITCR-3
	DEC Z,SCH
	LDA I,0		;SOUND ON
	STA A,SCREE
HITCR:	LDA Z,CRSH
	BNE S,+6
	LDA I,1		;SOUND OFF
	BNE SHTPRC-3
	DEC Z,CRSH
	LDA I,0		;SOUND ON
	STA A,CRASH
SHTPRC:	LDA Z,SHOT		;SHOT SOUND PROCESS
	BEQ SHOTOF
	DEC Z,SHOT
	LDA A,PIADA
	AND I,373
	STA A,PIADA
	JMP ZNG
SHOTOF:	LDA A,PIADA
	ORA I,4
	STA A,PIADA		;SHOT OFF
ZNG:	LDA Z,ZING		; TEST FOR ZING SOUND
	BEQ NOZNG
	DEC Z,ZING
	LDA A,PIADA
	AND I,357
	STA A,PIADA
	JMP BULLET
NOZNG:	LDA A,PIADA
	ORA I,20
	STA A,PIADA
BULLET:	LDA I,0
	STA Z,PNUM
	LDX I,PCB0
BULCY:	LDA Z,TIME+1
	LSR
	BCS BODDP
	LDA ZX,1
	BMI EVS
	LDA ZX,0		;CHECK PLAYER STATUS WORD FOR HIT
	AND I,20
	BNE BULCYC
	LDY ZX,5
	STY ZX,12
	LDA ZX,3
	STA ZX,10
EVS:	JSR BULPRC
BULCYC:	CPX I,PCB3
	BNE NBP
	LDA Z,AT
	BPL S,+3
	RTS
	JMP LP1
NBP:	TXA
	CLC
	ADC I,20
	TAX
	INC Z,PNUM
	BNE BULCY
BODDP:	LDA ZX,2
	BMI ODDS
	LDA ZX,0		;CHECK PLAYER STATUS WORD FOR HIT
	AND I,20
	BNE BULCYC
	LDY ZX,5		;DIRECTION CODE
	STY ZX,13
	LDA ZX,3
	STA ZX,11
ODDS:	INX
	JSR BULPRC
	DEX
	LDA ZX,2
	AND I,277		;CLEAR FIRE BIT
	STA ZX,2
	JMP BULCYC

BULPRC:	LDA ZX,1
	BMI MOVBL
	ASL
	ASL
	BCS S,+3
	RTS
	LDA I,1			;SHOT TIME COUNT
	STA Z,SHOT
	LDA ZX,10
	CLC
	ADC AY,BIV		;VERT
	STA ZX,10
	LDY Z,PNUM
	LDA AY,BIH		;HORZ
	STA ZX,6
	LDA ZX,1
	ORA I,200
	STA ZX,1
	RTS
; TEST HIT----------------------------------
MOVBL:	LDA I,37		;P0 H CHECK
	CMP ZX,6
	BCC P1CK		;A<M
	LDA ZX,10		;VP
	CMP Z,PCB0+3		;P0 VERT CHECK,TOP LINE
	BCC P1CK		;A<M
	SBC I,1
	CMP Z,PCB0+4		;P0 VERT CHECK,BOTTOM LINE
	BCS P1CK		;A>M
	LDA I,0
	CMP Z,PNUM		;TEST FOR HIT SELF
	BEQ S,+5		;MADE HIT, PROCESS
	JMP HITCAR
P1CK:	LDA Z,PNUM
	BNE P1CKA
P1CKA:	LDA ZX,6		;HP
	CMP I,40		;P1 H CHECK,LEFT
	BCS S,+5
	JMP MOVEBL
	SBC I,1
	CMP I,77		;P1 H CHECK,RIGHT
	BCS P2CK		;A>M
	LDA ZX,10		;VP
	CMP Z,PCB1+3		;P1 VERT CHECK, TOP LINE
	BCC P2CK		;A<M
	SBC I,1
	CMP Z,PCB1+4		;P1 VERT CHECK, BOTTOM LINE
	BCS P2CK		;A>M
	LDA I,1
	CMP Z,PNUM		;TEST FOR HIT SELF
	BEQ S,+5		;MADE HIT,PROCESS
	JMP HITCAR
P2CK:	LDA Z,SING		
	BPL S,+10
	LDA Z,PNUM
	CMP I,3
	BEQ PLCK
	LDA ZX,6		;HP
	CMP I,300		;P2 H CHECK,LEFT
	BCC P3CK		;A<M
	SBC I,1
	CMP I,337		;P2 H CHECK,RIGHT
	BCS P3CK		;A>M
	LDA ZX,10		;VP
	CMP Z,PCB2+3		;P2 VERT CHECK,TOP LINE
	BCC P3CK		;A<M
	SBC I,1
	CMP Z,PCB2+4		;P2 VERT CHECK,BOTTOM LINE
	BCS P3CK		;A>M
	LDA I,2
	CMP Z,PNUM		;TEST HIT SELF
	BNE HITCAR
P3CK:	LDA ZX,6		;HP
	CMP I,340		;P3 H CHECK,LEFT
	BCC PLCK		;A<M
	SBC I,1
	CMP I,377		;P3 H CHECK,RIGHT
	BCS PLCK		;A>M
	LDA ZX,10		;VP
	CMP Z,PCB3+3		;P3 VERT CHECK, TOP LINE
	BCC PLCK		;A<M
	SBC I,1
	CMP Z,PCB3+4		;P3 VERT CHECK, BOTTOM LINE
	BCS PLCK		;A>M
	LDA I,3
	CMP Z,PNUM		;TEST FOR HIT SELF
	BNE HITCAR		;MADE HIT, PROCESS
PLCK:	LDA ZX,6		;HP
	CMP I,170		;PAUL H CHECK,LEFT
	BCS S,+5		;MOVE BULLET
	JMP MOVEBL
	SBC I,1
	CMP I,210		;PAUL H CHECK, RIGHT
	BCC S,+5
	JMP MOVEBL		;A>M, MOVE BULLET
	LDA ZX,10		;VP
	CMP Z,PAULA+1		;PAUL VERT CHECK, TOP LINE
	BCC PBTST		;A<M, MOVE BULLET
	SBC I,1
	CMP Z,PAULA+2		;PAUL VERT CHECK, BOTTOM LINE
	BCC BULSTP		;A>M, MOVE BULLET
PBTST:	LDA Z,PAULB
	BMI S,+5
	JMP MOVEBL
	LDA ZX,10
	CMP Z,PAULB+1
	BCC PCTST
	SBC I,1
	CMP Z,PAULB+2
	BCC BULSTP
PCTST:	LDA Z,PAULC
	BPL MOVEBL
	LDA ZX,10
	CMP Z,PAULC+1
	BCC PDTST
	SBC I,1
	CMP Z,PAULC+2
	BCC BULSTP
PDTST:	LDA Z,PAULD
	BPL MOVEBL
	LDA ZX,10
	CMP Z,PAULD+1
	BCC MOVEBL
	SBC I,1
	CMP Z,PAULD+2
	BCS MOVEBL
BULSTP:	LDA ZX,1		;HIT TRUCK, STOP BULLET
	AND I,177		;CLEAR BULLET ACTIVE
	STA ZX,1
	RTS
;	(A)= CAR # HIT
HITCAR:	TAY
	ASL
	ASL
	ASL
	ASL
	STX Z,R0		;SAVE X
	TAX
	LDA I,20
	AND ZX,0
	BEQ S,+7
	LDX Z,R0
	JMP BULSTP		;HIT CAR ALREADY DOWN
	LDA I,120		;SCREECH TIME COUNT
	STA Z,SCH
	LDA I,1
	STA AY,MOTOR		;MOTOR OFF
	LDA ZX,220
	ADC Z,R0
	STA ZX,220
	LDA ZX,221
	ADC Z,R1
	STA ZX,221
	LDA ZX,0		;SET PLAYER HIT & COUNT
	ORA I,37
	AND I,237
	STA ZX,0
	LDA I,16		;SIDE PICTURE
	STA ZX,5
	TXA
	AND I,40
	BEQ SC23		;HIT PLAYERS 1 &/OR 2
SC01:	LDX I,0			;SCORE P1 & P2
SCALL:	JSR SCORE
	LDX Z,R0
	JMP BULSTP
SC23:	LDX I,2			;SCORE P2 & P3
	BNE SCALL
MOVEBL:	LDY ZX,12		;DIRECTION CODE TO Y
	LDA ZX,10		;VERT POS TO A
	CMP I,10		;TEST TOP OF SCREEN
	BCS CKBOT
BMOV:	LDA Z,PNUM		;VERT = 0
	CMP I,0			;PLAYER 0
	BNE CK3
RIC0:	LDA AY,RICTBL
	STA ZX,12
	TAY
	LDA I,1
	STA Z,ZING
	LDA ZX,10
	JMP VSTP
CK3:	CMP I,3			;PLAYER 3
	BEQ RIC0
BEND:	LDA ZX,1
	AND I,100		;SAVE FBUTTON BIT
	STA ZX,1
	RTS
CKBOT:	CMP I,310		;VERT BOTTOM = 353
	BCS BMOV
VSTP:	CLC
	ADC AY,DRTBLV
	STA ZX,10
	LDA ZX,6
	CMP I,7
	BCC BEND
	CMP I,371
	BCS BEND
	CLC
	ADC AY,DRTBLH
	STA ZX,6
	RTS

BIV:	.BYTE 16,20,20,23,24,24,26	;OFFSET FOR DIR. CODE FOR CODES 0-6
	.BYTE 16,20,20,23,24,24,26	;OFFSET FOR DIR. CODE FOR CODES 7-15
BIH:	.BYTE 36			;HORZ. POS. FOR PLAYER 0
	.BYTE 76			;HORZ. POS. FOR PLAYER 1
	.BYTE 303			;HORZ. POS. FOR PLAYER 2
	.BYTE 343			;HORZ. POS. FOR PLAYER 3
RICTBL:	.BYTE 6
	.BYTE 5
	.BYTE 4
	.BYTE 12
	.BYTE 2
	.BYTE 1
	.BYTE 0
	.BYTE 15
	.BYTE 14
	.BYTE 13
	.BYTE 3
	.BYTE 11
	.BYTE 10
	.BYTE 7
DRTBLV:	.BYTE 374,376,376,0,2,2,4,374,376,376,0,2,2,4
DRTBLH:	.BYTE 4,4,6,6,6,4,4,374,374,372,372,372,374,374

;GAME TIMES:  1:0, 1:40, 2:20, 3:0 MIN:SEC
TYMTBL:	.BYTE 63,65,67,71

SWTBL:	.BYTE 0,4,10,14

COININ:	LDA Z,AT
	BMI S,+3
	RTS
CONIN:	LDA A,COIN1
	AND A,COIN2
	BPL S,+3
	RTS
	LDA I,17
	STA Z,R0
	STA Z,R1
CNLP:	LDA A,COIN1
	BMI C1NT
	LDA Z,R0
	BEQ C2CK
C1CK:	DEC Z,R0
	BNE C2CK
	INC Z,COINS
	BNE CNLP0
C1NT:	LDA I,17
	STA Z,R0
C2CK:	LDA I,310		;1 MSEC DELAY
	STA Z,R4
	DEC Z,R4
	BNE S,-2
	LDA A,COIN2
	BMI C2NT
	LDA Z,R1
	BEQ CNLP0
C2PRC:	DEC Z,R1
	BNE CNLP0
	INC Z,COINS
	BNE CNLP0
C2NT:	LDA I,17
	STA Z,R1
CNLP0:	LDA A,COIN1
	AND A,COIN2
	BPL CNLP
	RTS

SCRINT:	LDA I,60
	LDX I,4
	STA ZX,SCR-1		;INITIALIZE SCORES
	DEX
	BNE S,-3
	RTS

CLRP:	LDA I,164
	STA A,PIADA
	LDX I,0
	LDA I,0
	STA A,PAULS
	STA AX,BP
	STA AX,PV
	STA AX,PF
	STA AX,PF+400
	STA AX,PF+1000
	STA AX,PF+1400
	STA AX,PAULV
	INX
	BNE S,-26
	LDX I,4
	STA AX,PD-1
	STA AX,PS-1
	DEX
	BNE S,-7
	LDX I,360
	STA ZX,PCB0
	DEX
	BNE S,-3
	LDA I,124
	STA A,PIADA
	RTS

PALST:	.BYTE 140,40,240

INITP:	LDA I,164			;ENABLE BUS
	STA A,PIADA
	LDA I,PF&177400/400		;INIT. ROAD SIDES
	STA Z,R1
	LDA I,0
	STA Z,R0
	LDX I,36
SIDELN:	LDA I,72
	LDY I,10
	STA NY,R0
	LDY I,26
	STA NY,R0
	CLC
	LDA Z,R0
	ADC I,40
	STA Z,R0
	LDA Z,R1
	ADC I,0
	STA Z,R1
	DEX
	BNE SIDELN
	LDY I,0			;INITIALIZE ROAD WAYS
	LDX I,6
	LDA I,72
	STA Z,LINEHD
	STA Z,R0
LDN1:	LDA Z,R0
	STA AY,PF+3
	STA AY,PF+73
	STA AY,PF+303
	STA AY,PF+603
	STA AY,PF+1103
	STA AY,PF+1403
	STA AY,PF+373
	STA AY,PF+673
	STA AY,PF+1173
	STA AY,PF+1473
	DEX
	BEQ LNEDN
	CMP I,74
	BNE S,+6
	LDA I,71
	STA Z,R0
	INC Z,R0
	TYA
	CLC
	ADC I,40
	TAY
	JMP LDN1
LNEDN:	LDA I,73
	STA A,PF+33
	LDA I,1
	STA Z,PBIT
	LDA I,0
	STA Z,PNUM
	LDX I,PCB0
INPTST:	LDA ZX,0
	BPL INNXTP
	LDA I,40		;SET PLAYER LINE PICTURE BITS
	STA Z,R4
	LDY I,100
	STY ZX,3
	LDA I,137
	STA ZX,4
	LDA Z,PNUM
	CMP I,2
	BCC PC01
	LDA I,12
	BNE PC01+2
PC01:	LDA I,3
	STA ZX,5
SETCYC:	LDA AY,PV
	ORA Z,PBIT
	STA AY,PV
	INY
	DEC Z,R4
	BNE SETCYC
	LDY Z,PNUM
	LDA I,277
	STA AY,PS
INNXTP:	CPX I,PCB3
	BEQ INPAL
	CLC
	TXA
	ADC I,20
	TAX
	ASL Z,PBIT
	INC Z,PNUM
	BNE INPTST
INPAL:	LDX I,100
	STX Z,PAULA+1
	LDA I,137
	STA Z,PAULA+2
	STA AX,PAULV
	INX
	CPX I,140
	BNE S,-6
	LDA I,200
	STA Z,PAULA
	LDA I,277
	STA Z,PAULA+3
	STA A,PAULS
	LDA I,124		;DISABLE BUS
	STA A,PIADA
	INC A,PIADA
	RTS

SCORE:	LDA Z,AT
	BPL S,+3
	RTS
	LDA I,72		;UPDATE SCORE
	INC ZX,SCR
	CMP ZX,SCR
	BEQ S,+3
	RTS
	INC ZX,SCR+1
	CMP ZX,SCR+1
	BEQ S,+7
	LDA I,60
SCOREA:	STA ZX,SCR
	RTS
	LDA I,60
	STA ZX,SCR+1
	BNE SCOREA

SOUND:	LDA A,MES30+3
	CMP A,BIV+4
	BEQ S,+3
	BRK
	LDA ZX,0
	AND I,37
	BNE S,+3
	RTS
	CMP I,30
	BNE HITOVR
	LDA I,100
	STA Z,CRSH
	INC ZX,5		;CHANGE PICTURES
	DEC ZX,0
	RTS
HITOVR:	CMP I,20		;TEST FOR COUNT DONE
	BNE HITOVR-3
	LDA Z,AT
	BMI HO
	TXA
	AND I,70
	LSR
	LSR
	LSR
	LSR
	TAY
	LDA Z,SING
	BPL HLF
	CPY I,2
	BCS HO
HLF:	LDA AY,SWTBL
	TAY
	LDA AY,P1SW		;TEST GAS PEDAL DEPRESSED
	BPL S,+3
	RTS
HO:	LDA ZX,0
	AND I,340
	STA ZX,0
	LDA Z,TIME+1
	CMP I,10		;TEST ABOVE TOP LINE
	BCS GOTTOP
	ADC I,1
	BNE S,-6
GOTTOP:	CMP I,245		;TEST OVER BOTTOM LINE
	BCC GOTBOT
	SBC I,1
	BNE GOTTOP
GOTBOT:	TAY
	STA Z,R1
	ADC I,40
	STA Z,R0
	LDA A,SYNC
	BMI S,-3
	LDA A,SYNC
	BPL S,-3
SPLY:	LDA AY,PV
	ORA Z,PBIT
	STA AY,PV
	INY
	CPY Z,R0
	BNE SPLY
	LDY Z,PNUM
	LDA Z,R1
	EOR I,377
	STA AY,PS
	LDA Z,R1
	STA ZX,3
	LDA Z,R0
	SEC
	SBC I,1
	STA ZX,4
	LDA Z,PNUM
	AND I,2
	BNE S,+6
	LDA I,3
	BNE S,+4
	LDA I,12
	STA ZX,5
	RTS

SWIT:	LDX I,PCB0
	LDY I,0
SWTLP:	LDA ZX,0
	BPL PSNXT
	LDA AY,P1SW		;GET PLAYER SWITCH WORD
	STA ZX,17
PSNXT:	CPX I,PCB3
	BEQ PATCK
	CLC
	TXA
	ADC I,20
	TAX
	INY
	INY
	INY
	INY
	BNE SWTLP
PATCK:	LDA Z,AT
	BMI OKAT
	LDA Z,SING
	BMI OK01
	BPL NOAT
OKAT:	LDX I,PCB0
	JSR PLYAT
	LDA Z,PCB3+3
	JSR ATRIF
	LDX I,PCB1
	JSR PLYAT
	LDA Z,PCB2+3
	JSR ATRIF
OK01:	LDX I,PCB2
	JSR PLYAT
	LDA Z,AT
	BMI TRK2
	LDA Z,PCB0+3
	JMP OKTRK
TRK2:	LDA Z,PCB1+3
OKTRK:	JSR ATRIF
	LDX I,PCB3
	JSR PLYAT
	LDA Z,PCB0+3
	JSR ATRIF
NOAT:	LDX I,PCB0
	JSR PSCOMP
	LDX I,PCB1
	JSR PSCOMP
	LDX I,PCB2
	JSR PSCOMP
	LDX I,PCB3
	JSR PSCOMP
DOBULL:	LDA Z,TIME+1
	AND I,3			;FIRE EVERY 4TH FRAME
	TAY
	ASL
	ASL
	ASL
	ASL
	TAX
	LDA ZX,0
	BMI S,+3		;PLAYER ACTIVE
	RTS
	AND I,20
	BEQ S,+3		;& PLAYER NOT HIT
	RTS
	LDA Z,SING
	BPL ATCK
	LDA Z,PCB3+1
	ORA I,100
	STA Z,PCB3+1
	LDA Z,PCB2+1
	ORA I,100
	STA Z,PCB2+1
	CPY I,0
	BEQ ATCK
	RTS
ATCK:	LDA Z,AT
	BPL S,+11
	LDA ZX,1
	ORA I,100
	STA ZX,1
	RTS
	LDA I,200		;FIRE BUTTON MASK SET
	DEY
	BMI S,+5
	LSR
	BNE S,-4
	AND A,PFIRE		;TEST PLAYERS FIRE BUTTON
	BEQ FSET		;FIRE BUTTON PUSHED
	LDA ZX,1		;CLEAR FBUTTON FLAG
	AND I,277
	STA ZX,1
	RTS
FSET:	LDA ZX,1
	BMI FB1ACT		;#1 ACTIVE?
	ASL
	ASL
	BCC S,+3
	RTS
	LDA ZX,1
	ORA I,100		;SET FIRE #1
	STA ZX,1
	RTS
FB1ACT:	ASL			;TEST FBUTTON FLAG
	ASL
	BCC S,+3		;NOT ON
	RTS
	LDA ZX,2
	BPL S,+3		;#2 ACTIVE?
	RTS
	ORA I,100		;SET FIRE #2
	STA ZX,2
	LDA ZX,1
	ORA I,100
	STA ZX,1
	RTS

ATRIF:	TAY
	LDA ZX,0
	AND I,20
	BEQ S,+3
	RTS
	TYA
	SEC
	SBC ZX,3
	BCC NEGRIF
POSRIF:	ROL
	ROL
	ROL
	ROL
	AND I,7
	TAY
	LDA AY,PLUS
	TAY
	TXA
	AND I,40
	BNE RIF3
	TYA
	AND I,17
	STA ZX,5
	RTS
RIF3:	TYA
	AND I,360
	LSR
	LSR
	LSR
	LSR
	STA ZX,5
	RTS
NEGRIF:	EOR I,377
	ADC I,1
	ROL
	ROL
	ROL
	ROL
	AND I,7
	TAY
	LDA AY,MINUS
	TAY
	TXA
	AND I,40
	BNE RIF2
	TYA
	AND I,17
	STA ZX,5
	RTS
RIF2:	TYA
	AND I,360
	LSR
	LSR
	LSR
	LSR
	STA ZX,5
	RTS
PLUS:	.BYTE 243,264,264,305,305,326,326,326
MINUS:	.BYTE 243,222,222,201,201,160,160,160
PLYAT:	LDA ZX,15		;UP COUNTER
	BNE ATUP
	LDA ZX,16		;DOWN COUNTER
	BNE ATDWN
	LDA ZX,220
	ADC Z,TIME+1
	STA ZX,220
	AND I,177
	STA ZX,15
	LDA ZX,221
	ADC Z,TIME+1
	STA ZX,221
	AND I,177
	STA ZX,16
ATUP:	DEC ZX,15
	LDA I,0
	STA ZX,17
	RTS
ATDWN:	DEC ZX,16
	LDA I,200
	STA ZX,17
	RTS

PSCOMP:	LDA ZX,0
	BMI S,+3
	RTS
	ASL
	ASL
	ASL
	ASL
	BCC NOHIT
	LDA ZX,3
	CMP I,377		;CHECK TOP LINE OFF SCREEN
	BNE S,+3
	RTS
	LDA ZX,0
	ORA I,100		;SET MOTION DOWN WHEN HIT
	STA ZX,0
	RTS
NOHIT:	LDA ZX,0
	ASL ZX,17
	BCS PDWN
	LDY ZX,3
	CPY I,10
	BNE PUP
RLP4:	LDA ZX,0
	AND I,237
	STA ZX,0
	BNE RIF
PUP:	ORA I,140		;SET MOTION UP TO PSW
	STA ZX,0
RIF:	LDA Z,AT
	BPL S,+3
	RTS
	TXA
	AND I,40
	BNE P34RIF
	LDY I,0
RIFDEV:	LDA ZX,17
	EOR I,376
	BNE S,+3
	RTS
RIFLPA:	ASL
	BCS MULCK
	INY
	BNE RIFLPA
MULCK:	BEQ S,+3
	RTS
	STY ZX,5
	RTS
P34RIF:	LDA Z,SING
	BPL S,+3
	RTS
	LDY I,7
	BNE RIFDEV
PDWN:	LDY ZX,4
	CPY I,304
	BEQ RLP4
	ORA I,100
	BNE RIF-2


OUTOBJ:	LDA I,0
	STA Z,PNUM
	LDA I,1
	STA Z,PBIT
	LDA I,20
	STA Z,PBBIT
	LDA I,357
	STA Z,PBNBIT
	LDA I,376
	STA Z,PNBIT
	LDA A,SYNC
	BMI S,-3
	LDA A,SYNC
	BPL S,-3
	LDX I,PCB0
OUTLP1:	LDA ZX,0
	BMI OUTPLY		;TEST PLAYER ACTIVE
OUTLP2:	CPX I,PCB3
	BNE OUTLP3
	LDA Z,TIME+1
	AND I,3
	BEQ S,+3
	RTS
	JMP OUTPL
OUTLP3:	CLC
	TXA
	ADC I,20
	TAX
	INC Z,PNUM
	ASL Z,PBIT
	ASL Z,PBBIT
	SEC
	ROL Z,PNBIT
	SEC
	ROL Z,PBNBIT
	BNE OUTLP1
OUTPLY:	ASL
	ASL
	BCC OUTPIC		;TEST FOR MOTION
	ASL
	BCS OUTUP
	LDY Z,PNUM
	LDA I,1
	STA AY,MOTOR		;MOTOR OFF
	JMP OUTDWN
OUTUP:	LDY Z,PNUM
	LDA I,0
	STA AY,MOTOR		;MOTOR ON
	LDY ZX,4		;BOTTOM LINE
	LDA AY,PV
	AND Z,PNBIT		;CLEAR PLAYER BIT
	STA AY,PV		;RETURN LINE WORD TO RAM
	DEY
	STY ZX,4
	LDY ZX,3		;TOP LINE
	DEY
	LDA AY,PV
	ORA Z,PBIT		;SET PLAYER BIT TO NEW LINE
	STA AY,PV
	STY ZX,3
	TYA
	EOR I,377
	LDY Z,PNUM
	STA AY,PS		;RESET SYNC OFFSET
OUTPIC:	LDY Z,PNUM
	LDA ZX,5
	STA AY,PD		;PICTURE CODE OUT
	LDA ZX,0
	AND I,237
	STA ZX,0
	LDA Z,TIME+1
	LSR
	BCC EVEN
ODD:	LDY ZX,6		;B#0 ACTIVE
	LDA AY,BP		;HORZ.
	AND Z,PNBIT
	STA AY,BP		;CLEAR HORZ. B#0
	LDA AY,BP+1
	AND Z,PNBIT
	STA AY,BP+1
	LDY ZX,10
	LDA AY,BP		;VERT.
	AND Z,PBNBIT
	STA AY,BP
	LDA AY,BP+1
	AND Z,PBNBIT
	STA AY,BP+1
	LDA ZX,2
	BMI S,+5
	JMP OUTLP2
SODD:	LDY ZX,7
	LDA AY,BP		;HORZ.
	ORA Z,PBIT
	STA AY,BP
	LDA AY,BP+1
	ORA Z,PBIT
	STA AY,BP+1
	LDY ZX,11		;VERT.
	LDA AY,BP
	ORA Z,PBBIT
	STA AY,BP
	LDA AY,BP+1
	ORA Z,PBBIT
	STA AY,BP+1
	JMP OUTLP2
EVEN:	LDY ZX,7		;B#1 ACTIVE
	LDA AY,BP		;HORZ.
	AND Z,PNBIT
	STA AY,BP
	LDA AY,BP+1
	AND Z,PNBIT
	STA AY,BP+1
	LDY ZX,11		;VERT.
	LDA AY,BP
	AND Z,PBNBIT
	STA AY,BP
	LDA AY,BP+1
	AND Z,PBNBIT
	STA AY,BP+1
	LDA ZX,1
	BMI SEVEN
	JMP OUTLP2
SEVEN:	LDY ZX,6		;HORZ.
	LDA AY,BP
	ORA Z,PBIT
	STA AY,BP
	LDA AY,BP+1
	ORA Z,PBIT
	STA AY,BP+1
	LDY ZX,10		;VERT.
	LDA AY,BP
	ORA Z,PBBIT
	STA AY,BP
	LDA AY,BP+1
	ORA Z,PBBIT
	STA AY,BP+1
	JMP OUTLP2
OUTDWN:	LDY ZX,3
	LDA AY,PV
	AND Z,PNBIT		;CLEAR PLAYER BIT
	STA AY,PV
	INY			;+1 TO TOP LINE #
	TYA
	STA ZX,3
	EOR I,377
	LDY Z,PNUM
	STA AY,PS		;RESET SYNC OFFSET
	LDY ZX,4
	LDA ZX,0
	AND I,20
	BEQ OUTDNC
	CPY I,377
	BNE S,+5
	JMP OUTPIC
OUTDNC:	INY
	LDA AY,PV		;BOTTOM LINE
	ORA Z,PBIT		;SET NEW BOTTOM LINE
	STA AY,PV
	STY ZX,4
	JMP OUTPIC
OUTPL:	LDA Z,SCR		;OUTPUT SCORES
	STA A,PF+53
	LDA Z,SCR+1
	STA A,PF+54
	LDA Z,SCR+2
	STA A,PF+63
	LDA Z,SCR+3
	STA A,PF+64
	LDA Z,AT
	BMI PLOOP-2
	LDA Z,GTIMA
	STA A,PF+1512
	LDA Z,GTIMB
	STA A,PF+1513
	LDX I,PAULA
PLOOP:	LDA ZX,0
	BMI PAULPR
PLOOPA:	CPX I,PAULD
	BEQ ROADMV
	INX
	INX
	INX
	INX
	JMP PLOOP
PAULPR:	LDY ZX,1		;TOP LINE PROCESS
	DEY
	LDA I,20
	STA AY,PAULV
	STY ZX,1
	TYA
	EOR I,377
	STA A,PAULS
	STY ZX,3
	LDY ZX,2
	LDA I,0
	STA AY,PAULV
	DEY
	STY ZX,2
	JMP PLOOPA
ROADMV:	LDY I,0			;MOVE BOTH ROAD WAYS ONE STEP
	LDX I,6
	LDA Z,LINEHD
	CMP I,74
	BNE S,+4
	LDA I,71
	STA Z,R0
	INC Z,R0
	LDA Z,R0
	STA Z,R1
	CMP I,74
	BNE S,+6
	LDA I,71
	STA Z,R1
	INC Z,R1
MRL:	LDA Z,R0		;MOVE P0&P1 ROAD
	STA AY,PF+3
	STA AY,PF+303
	STA AY,PF+603
	STA AY,PF+1103
	STA AY,PF+1403
	LDA Z,R1
	STA AY,PF+33
	STA AY,PF+333
	STA AY,PF+633
	STA AY,PF+1133
	STA AY,PF+1433
	DEX
	BEQ MRLDN
	CMP I,74
	BNE S,+6
	LDA I,71
	STA Z,R1
	INC Z,R1
	LDA Z,R0
	CMP I,74
	BNE S,+6
	LDA I,71
	STA Z,R0
	INC Z,R0
	TYA
	CLC
	ADC I,40
	TAY
	JMP MRL
MRLDN:	INC Z,LINEHD
	LDA Z,LINEHD
	CMP I,75
	BEQ S,+3
	RTS
	LDA I,71
	STA Z,LINEHD
	LDA A,MES30+4
	CMP A,SWIT-22
	BEQ S,+3
	BRK
	RTS
MESOUT:	LDA AX,MESTBI		;(X) = MESSAGE # FOR MESSAGE OUT
	STA Z,MESI
	LDA AX,MESTBI+1
	STA Z,MESI+1
	LDA AX,MESTBO
	STA Z,MESO
	LDA AX,MESTBO+1
	STA Z,MESO+1
	LDY I,0
	LDA A,SYNC
	BMI S,-3
	LDA A,SYNC
	BPL S,-3
MES1:	LDA NY,MESI
	CMP I,EOT
	BNE S,+3
	RTS
	STA NY,MESO
	INY
	JMP MES1

	.EVEN
MESTBI:	.WORD MES0		;GAME OVER
	.WORD MES2		;COPS
	.WORD MES4		;ROBBERS
	.WORD MES6		;TIMER
	.WORD MES10
	.WORD MES12
	.WORD MES14
	.WORD MES16
	.WORD MES26
	.WORD MES24
	.WORD MES22
	.WORD MES20
	.WORD MES30
	.WORD MES32
	.WORD MES34
	.WORD MES36
	.WORD MES40
	.WORD MES42
	.WORD MES44
	.WORD MES46
	.WORD MES50
	.WORD MES52
MESTBO:	.WORD PF+513		;GAME OVER
	.WORD PF+22		;COPS
	.WORD PF+10		;ROBBERS
	.WORD PF+1351		;TIMER
	.WORD PF+513
	.WORD PF+605
	.WORD PF+302
	.WORD PF+1341
	.WORD PF+745
	.WORD PF+745
	.WORD PF+745
	.WORD PF+745
	.WORD PF+1361		;ATARI
	.WORD PF+205		;SING. P. CAR LOC.
	.WORD PF+314
	.WORD PF+1210
	.WORD PF+1251
	.WORD PF+1111
	.WORD PF+1155
	.WORD PF+255
	.WORD PF+611		;ONE START
	.WORD PF+604		;MULTI START

	;GAME OVER
MES0:	.BYTE 22,5,26,17,0,0,5,15,1,7,EOT
	;COPS
MES2:	.BYTE 23,20,17,3,EOT
	;ROBBERS
MES4:	.BYTE 23,22,5,2,2,17,22,EOT
	;TIMER
MES6:	.BYTE 5,15,11,24,EOT
MES10:	.BYTE 0,0,0,0,0,0,0,0,0,0,EOT

	;PUSH SINGLE OR MULTIPLE START
MES12:	.BYTE 24,22,1,24,23,0,11,24,14,25,15,0,22
	.BYTE 17,0,5,16,17,0,10,23,25,20,EOT

	;INSERT COIN FOR MORE PLAYERS
MES14:	.BYTE 23,22,5,31,1,14,20,0,5,22,17,15,0,22,17,6
	.BYTE 0,16,11,17,3,0,24,22,5,23,16,11,EOT

	;STEP ON GAS PEDAL, CAR APPEARS
MES16:	.BYTE 23,22,1,5,20,20,1,0,22,1,3,0,14,1,4
	.BYTE 5,20,0,23,1,7,0,16,17,0,20,5,24,23,EOT

	;25 CENTS PER PLAYER
MES20:	.BYTE 0,0,22,5,31,1,14,20,0,22,5,20,0,23,24,16,5,3,0,65,62,0,EOT

	;2 PLAYERS FOR 25 CENTS
MES22:	.BYTE 23,24,16,5,3,0,65,62,0,22,17,6,0,23,22,5,31,1,14,20,0,62,EOT

	;25 CENTS PER GAME
MES24:	.BYTE 0,0,0,5,15,1,7,0,22,5,20,0,23,24,16,5,3,0,65,62,0,0,EOT

	;2 COINS PER PLAYER
MES26:	.BYTE 0,0,0,0,22,5,31,1,14,20,0,22,5,20,0,23,16,11,17,3,0,62,EOT

	;ATARI
MES30:	.BYTE 11,22,1,24,1,EOT

	;SINGLE PLAYER CAR LOCATION
MES32:	.BYTE 24,10,7,11,22,0,16,17,0,22,1,3,0,22,5,31,1,14,20,0
	.BYTE 5,14,7,16,11,23,EOT

	;1 TO 4 PLAYERS
MES34:	.BYTE 23,22,5,31,1,14,20,EOT

	 ;AGAINST MACHINE
MES36:	.BYTE 5,16,11,10,3,1,15,0,24,23,16,11,1,7,1,EOT

	;WHEN CAR HIT
MES40:	.BYTE 24,11,10,0,22,1,3,0,16,5,10,27,EOT

	;SINGLE PLAYER
MES42:	.BYTE 22,5,31,1,14,20,0,5,14,7,16,11,23,EOT

	;PLAYS
MES44:	.BYTE 23,31,1,14,20,EOT

	;1 TO 4
MES46:	.BYTE 64,0,17,24,0,61,EOT

	;PUSH ONE START
MES50:	.BYTE 24,22,1,24,23,0,5,16,17,0,10,23,25,20,EOT

	;PUSH MULTI START
MES52:	.BYTE 0,0,0,0,24,22,1,24,23,0,11,24,14,25,15,0,10,23,25,20,0,0,0,0,EOT
	.=17770
	.WORD START
	.WORD START
	.WORD START
	.WORD START
CREND:	.END START
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                    